#!@@PERL@@
# -*- perl -*-

=head1 NAME

postfix_mailstats - Plugin to monitor the number of mails delivered and
rejected by postfix

=head1 CONFIGURATION

Configuration parameters for @@CONFDIR@@/postfix_mailstats,
if you need to override the defaults below:

 [postfix_mailstats]
  env.logdir  - Which logfile to use
  env.logfile - What file to read in logdir
  env.postconf - Path to postconf binary

=head2 DEFAULT CONFIGURATION

 [postfix_mailstats]
  env.logdir  /var/log
  env.logfile mail.log

=head1 AUTHOR

Records show that the plugin was contributed by Nicolai Langfeldt in
2003.  Nicolai can't find anything in his email about this and expects
the plugin is based on the corresponding exim plugin - to which it now
bears no resemblence.

=head1 LICENSE

GPLv2

=head1 MAGIC MARKERS

=begin comment

These magic markers are used by munin-node-configure when installing
munin-node.

=end comment

 #%# family=auto
 #%# capabilities=autoconf

=cut

use strict;
use warnings;
use Munin::Plugin;

my $LOGDIR  = (defined($ENV{'logdir'}) ? $ENV{'logdir'} : '/var/log');
my $LOGFILE = (defined($ENV{'logfile'}) ? $ENV{'logfile'} : 'mail.log');
my $POSTCONF = (defined($ENV{'postconf'}) ? $ENV{'postconf'} : 'postconf');
my $logfile = "$LOGDIR/$LOGFILE";

if ( defined($ARGV[0]) and $ARGV[0] eq "autoconf" )
{
    `which $POSTCONF >/dev/null 2>/dev/null`;
    if (!$?)
    {
        my (undef, $s) = testfile($logfile,"autoconf");
        print "$s\n"
    }
    else
    {
        print "no (postfix not found)\n";
    }
    exit 0;
}

my @state = restore_state();

my $pos = shift @state;

my $delivered = shift @state;

my %rejects = @state;

if ( $ARGV[0] and $ARGV[0] eq "config" )
{
    print "graph_title Postfix message throughput\n";
    print "graph_args --base 1000 -l 0\n";
    print "graph_vlabel mails / \${graph_period}\n";
    print "graph_scale  no\n";
    print "graph_total  Total\n";
    print "graph_category postfix\n";
    print "graph_period minute\n";
    print "delivered.label delivered\n";
    print "delivered.type DERIVE\n";
    print "delivered.draw AREA\n";
    print "delivered.min 0\n";
    foreach my $i (sort keys %rejects)
    {
	    print "r$i.label reject $i\n";
	    print "r$i.type DERIVE\n";
	    print "r$i.draw STACK\n";
	    print "r$i.min 0\n";
    }
    exit 0;
}

if (! -f $logfile)
{
    print "delivered.value U\n";
    foreach my $i (sort keys %rejects)
    {
    	print "r$i.value U\n";
    }
    exit 0;
}

my $startsize = (stat $logfile)[7];
# Initial run.
$pos = $startsize unless defined($pos);

if ($startsize < $pos)
{
    # Log rotated
    my $rotlogfile = rotlog($logfile);
    if ( testfile($rotlogfile))
    {
        parseLogfile($rotlogfile, $pos, (stat $rotlogfile)[7]);
    }
    $pos = 0;
}

$pos = parseLogfile($logfile, $pos, $startsize);

$delivered = 0 unless defined($delivered);

print "delivered.value $delivered\n";

foreach my $i (sort keys %rejects)
{
	print "r$i.value ", $rejects{$i}, "\n";
}

save_state($pos, $delivered, %rejects);

sub parseLogfile
{
    my ($fname, $start, $stop) = @_;

    my ($logfd, $reset) = tail_open($fname, $start);

    while (tell($logfd) < $stop)
    {
        my $line = <$logfd>;
        chomp ($line);

        if ($line =~ /qmgr.*from=.*size=[0-9]*/)
        {
	        $delivered++;
        }
        elsif ($line =~ /postfix\/smtpd.*proxy-reject: \S+ (\S+)/ ||
           $line =~ /postfix\/smtpd.*reject: \S+ \S+ \S+ (\S+)/ ||
           $line =~ /postfix\/postscreen.*reject: \S+ \S+ \S+ (\S+)/ ||
           $line =~ /postfix\/cleanup.* reject: (\S+)/)
        {
	        $rejects{$1}++;
        }
    }
    return tail_close($logfd);
}

# vim:syntax=perl
