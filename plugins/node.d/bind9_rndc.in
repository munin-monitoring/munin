#!@@PERL@@ -w
# -*- perl -*-

=head1 NAME

bind9_rndc - Plugin to monitor usage of bind 9 servers using rndc stats

=head1 CONFIGURATION

The following environment variables are used by this plugin

  [bind_rndc]
    env.rndc		/usr/sbin/rndc
    env.querystats      /var/run/named.stats

The user/group that runs the plugin must have read access to the stats
file.  To change user or group (usually Munin plugins are run as
nobody) add this to the [bind_rndc] stanza if the "bind" user runs
BIND:

    user bind

On the BIND side put

  statistics-file "/var/run/named.stats";

in the options part of your named.conf or set the querystats variable
(see below) to where your named puts the statistics file by default.

You must also make sure the rndc.key file is readable by the user that
runs the plugin.

=head1 FEATURES AND BUGS

Previous versions of this plugin allowed a empty "rndc" environment
setting to not do a explicit dump of stats to the stats file.  This
version requires running rndc itself.  This makes the method of
finding the correct stats in the file more reliable than before.

=head1 AUTHOR

Contributed by Laurent Facq 15/06/2004.  Based on Nicolai Langfeldts
bind9 plugin.  Reworked by Dagfinn Ilmari MannsÃ¥ker.  BIND 9.6 patch
from "Vrivellino".

=head1 LICENSE

License not documented.

=head1 MAGIC MARKERS

 #%# family=manual

=cut

use strict;

my $rndc = defined($ENV{rndc}) ? $ENV{rndc} : '/usr/sbin/rndc';
my $querystats = $ENV{querystats} || '/var/run/named.stats';
my %IN;
my $version96 = 0;

# check to see if we're running bind 9.6
if ( open VERSION, "$rndc 2>&1 |" ) {
    while ( my $line = <VERSION> ) {
	if ( $line =~ m/^Version:\s+9\.(\d+)\D/o ) {
	    $version96 = 1 if $1 >= 6;
	}
    }
}

# open the log file, and get its size
open(my $stats, '<', $querystats) or die "$0: $querystats: $!\n";
my $size = (stat $stats)[7];

# call rdnc and go directly to the correct offset
system("$rndc stats");
seek($stats , $size, 0);

# We want the last block like this in the file (bind 9.early)
#+++ Statistics Dump +++ (1087277501)
#success 106183673
#referral 2103636
#nxrrset 43534220
#nxdomain 47050478
#recursion 37303997
#failure 17522313
#--- Statistics Dump --- (1087277501)

# From BIND 9.5 or newer this is the format:
# 
# +++ Statistics Dump +++ (1222740363)
# ++ Incoming Requests ++
#                   13 QUERY
# ++ Incoming Queries ++
#                    9 A
#                    1 NS
#                    1 SOA
#                    1 MX
#                    1 TXT
# ++ Outgoing Queries ++
# ++ Name Server Statistics ++
#                   13 IPv4 requests received
#                   13 responses sent
#                   13 queries resulted in successful answer
#                    9 queries resulted in authoritative answer
#                    4 queries resulted in non authoritative answer
#                    4 queries caused recursion
# ++ Zone Maintenance Statistics ++
#                    6 IPv4 notifies sent
# --- Statistics Dump ---

if ( $version96 ) {
    my $found_stats = 0;
    while ( my $line = <$stats> ) {
	chomp $line;
 	
	if ( $found_stats ) {   
	    if ( $line =~ m/^\+\+/o ) {
		$found_stats = 0;
		next;
	    }
 	
	    if ( $line =~ m/^\s+(\d+) (.*)$/o ) {
		my $n = $1;
		my $msg = $2;
		
		if ($msg =~ m/requests received$/io) {
		    $IN{requests} = 0 unless $IN{requests};
		    $IN{requests} += $n;
 	
		} elsif ($msg =~ m/responses sent$/io ) {
		    $IN{responses} = 0 unless $IN{requests};
		    $IN{responses} += $n;
 	
		} elsif (lc $msg eq 'queries resulted in successful answer') {
		    $IN{success} = $n;
		    
		} elsif (lc $msg eq 
			  'queries resulted in authoritative answer') {
		    $IN{auth_answer} = $n;
		} elsif (lc $msg eq 
			  'queries resulted in non authoritative answer') {
		    $IN{nonauth_answer} = $n;
		} elsif (lc $msg eq 'queries resulted in nxrrset') {
		    $IN{nxrrset} = $n;
		} elsif (lc $msg eq 'queries resulted in servfail') {
		    $IN{failure} = $n;
		    
		} elsif (lc $msg eq 'queries resulted in nxdomain') {
		    $IN{nxdomain} = $n;
 	
		} elsif (lc $msg eq 'queries caused recursion') {
		    $IN{recursion} = $n;
 	
		} elsif (lc $msg eq 'duplicate queries received') {
		    $IN{duplicates} = $n;
		}
	    }
	    
	} elsif ( $line =~ m/^\+\+ Name Server Statistics \+\+$/o ) {
	    $found_stats = 1;
	    %IN = ( requests => 0,
		    responses => 0,
		    success => 0,
		    auth_answer => 0,
		    nonauth_answer => 0,
		    nxrrset => 0,
		    failure => 0,
		    nxdomain => 0,
		    recursion => 0,
		    duplicates => 0,
		);
	}
    }
} else { # not version 9.6

    while (my $line = <$stats>) {
	chomp $line;

	if ($line =~ m/\+\+\+ Statistics Dump \+\+\+/) {
	    # reset
	    undef %IN;
	} else {
	    my ($what, $nb)= split('\s+', $line);
	    if ($what && ($what ne '---')) {
		$IN{$what} = $nb;
	    }
	}

	next unless $line =~ /^(\w+) (\d+)$/;
	$IN{$1} = $2;
    }
}

close($stats);

if (defined($ARGV[0]) and ($ARGV[0] eq 'config')) {
    print "graph_title DNS Queries by status\n";
    print "graph_vlabel queries / \${graph_period}\n";
    print "graph_category BIND\n";

    for my $key (keys %IN) {
	print "query_$key.label $key\n";
	print "query_$key.type DERIVE\n";
	print "query_$key.min 0\n";
    }
} else {
    print "query_$_.value $IN{$_}\n" for keys %IN;
}
