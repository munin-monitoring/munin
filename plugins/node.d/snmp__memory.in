#!@@PERL@@ -w
# -*- perl -*-

=head1 NAME

snmp__memory - Munin plugin to monitor memory usage of a remote host
via SNMP.

=head1 CONFIGURATION

The following configuration variables are used

 host      - SNMP host to contact (default taken from link name)
 port      - SNMP port to use (default 161)
 community - SNMP community string to use (default "public")

=head1 NOTES

Based on snmp__df plugin.... If this plugin reports
different numbers from the snmp_winmem plugin it must be due
to snmp impementation quirks....

=head1 AUTHOR

Copyright (C) 2006 Lars Strand

=head1 LICENSE

This program is free software; you can redistribute it and/or
modify it under the terms of the GNU General Public License
as published by the Free Software Foundation; version 2 dated June,
1991.

This program is distributed in the hope that it will be useful,
but WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
GNU General Public License for more details.

You should have received a copy of the GNU General Public License
along with this program; if not, write to the Free Software
Foundation, Inc., 51 Franklin Street, Fifth Floor, Boston, MA 02110-1301 USA.

=head1 MAGIC MARKERS

 #%# family=snmpauto
 #%# capabilities=snmpconf

=cut

use strict;
use Net::SNMP;

my $DEBUG = 0;

my $host      = $ENV{host}      || undef;
my $port      = $ENV{port}      || 161;
my $community = $ENV{community} || "public";

my $response;

if (defined $ARGV[0] and $ARGV[0] eq "snmpconf")
{
    print "index   1.3.6.1.2.1.25.5.1.1.2.\n";
    print "require 1.3.6.1.2.1.25.5.1.1.2. [1-9]\n";
    print "require 1.3.6.1.2.1.25.2.2.0\n"; # memsize
    exit 0;
}

if ($0 =~ /^(?:|.*\/)snmp_([^_]+)_memory$/)
{
    $host  = $1;
    if ($host =~ /^([^:]+):(\d+)$/)
    {
	$host = $1;
	$port = $2;
    }
}
elsif (!defined($host))
{
    print "# Debug: $0 -- $1\n" if $DEBUG;
    die "# Error: couldn't understand what I'm supposed to monitor.";
}

# memory usage pr. process
my $hrSWRunPerfMem = "1.3.6.1.2.1.25.5.1.1.2.";

my ($session, $error) = Net::SNMP->session(
	   -hostname  => $host,
	   -community => $community,
	   -port      => $port
        );

if (!defined ($session))
{
    die "Croaking: $error";
}

# total memory
my $memsize = &get_single($session, "1.3.6.1.2.1.25.2.2.0") * 1024;

if (defined $ARGV[0] and $ARGV[0] eq "config")
{
    print "host_name $host\n";
    print "graph_title Memory usage\n";
    print "graph_category system\n";
    print "graph_vlabel Bytes\n";
    print "graph_info This grap shows memory usage.\n";

    # some devices reports negative memtotal value
    print "# Total memsize reported $memsize..." if $DEBUG;

    if ($memsize > 0) 
    {
	print "grap_args --base 1024 -l 0 --upper-limit $memsize\n";
    }
    else
    {
	print "grap_args --base 1024 -l 0\n";
    }

    print "memory.draw AREA\n";
    print "memory.label memory\n";
   
    exit 0;
}

my $processes = get_by_regex($session, $hrSWRunPerfMem, "[1-9]");

# the values
my $memtotal = 0;
while (my ($pid, $mem) = each(%$processes)) {
    $memtotal += $mem;
}

print "memory.value %d\n", $memtotal * 1024;

sub get_single
{
    my $handle = shift;
    my $oid    = shift;

    print "# Getting single $oid..." if $DEBUG;

    $response = $handle->get_request ($oid);

    if (!defined $response->{$oid})
    {
	print "undef\n" if $DEBUG;
	return undef;
    }
    else
    {
	print "\"$response->{$oid}\"\n" if $DEBUG;
	return $response->{$oid};
    }
}

sub get_by_regex
{
    my $handle = shift;
    my $oid    = shift;
    my $regex  = shift;
    my $result = {};
    my $num    = 0;
    my $ret    = $oid . "0";
    my $response;
    
    print "# Starting browse of $oid...\n" if $DEBUG;
    
    while (1)
    {
	if ($num == 0)
	{
	    print "# Checking for $ret...\n" if $DEBUG;
	    $response = $handle->get_request ($ret);
	}
	if ($num or !defined $response)
	{
	    print "# Checking for sibling of $ret...\n" if $DEBUG;
	    $response = $handle->get_next_request ($ret);
	}
	if (!$response)
	{
	    return undef;
	}
	my @keys = keys %$response;
	$ret = $keys[0];
	print "# Analyzing $ret (compared to $oid)...\n" if $DEBUG;
	last unless ($ret =~ /^$oid/);
	$num++;
	next unless ($response->{$ret} =~ /$regex/);
	@keys = split (/\./, $ret);
	$result->{$keys[-1]} = $response->{$ret};;
	print "# Index $num: ", $keys[-1], " (", $response->{$ret}, ")\n" if $DEBUG;
    };
    return $result;
}
